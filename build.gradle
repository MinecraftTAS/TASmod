// Setup plugin classpath
buildscript {
    repositories {
        maven { url = "https://maven.minecraftforge.net" }
        maven { url = "https://repo.spongepowered.org/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}

// Apply plugins
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'eclipse'

// Set basic properties
version = project.version
group = project.group
archivesBaseName = project.artifact

// Compile for java 8
sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

// Configure forge
minecraft {
	version = forge_version
	runDir = "run"
	mappings = mappings_version
    
    clientRunArgs += "--mixin mixins.tasmod.json --tweakClass org.spongepowered.asm.launch.MixinTweaker"
    serverRunArgs += "--mixin mixins.tasmod.json --tweakClass org.spongepowered.asm.launch.MixinTweaker"
}

// Dependency repositories
repositories {
    mavenCentral()
    maven { url = "https://jitpack.io" }
    maven { url = "https://maven.mgnet.work/main" }
    maven { url = 'https://repo.spongepowered.org/maven' }
}

// Dependency configurations
configurations {
    downloadMod {
    	transitive = false
    }
    embed
    compile.extendsFrom(embed)
}

// Dependencies
dependencies {
	// tasmod dependencies (exported with jar)
    embed group: 'com.dselent', name: 'bigarraylist', version: '1.0'
    embed('org.spongepowered:mixin:0.8.2') {
    	exclude module: 'launchwrapper'
    	exclude module: 'log4j-core'
        exclude module: 'commons-io'
        exclude module: 'gson'
        exclude module: 'guava'
    }
    
    // tasmod dependencies (only for compilation)
    compileOnly group: 'com.minecrafttas', name: 'killtherng', version: '2.0-SNAPSHOT'
    
    // tasmod mod dependencies (only for ide)
    downloadMod 'com.minecrafttas:killtherng-full:2.0-SNAPSHOT'
}

// Task for downloading KillTheRng
task downloadKTRNG(type: Copy) {
	group 'tasmod'
	description 'Downloads KillTheRNG to the run/mods/ folder of the project'
    from configurations.downloadMod
    into 'run/mods/'
}

// Update values from mcmod.info
processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", minecraft_version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'mod_id': id, 'mod_url': url, 'mod_name': name, 'mod_version': project.version,
                'mcversion': minecraft_version, 'mod_description': description,
                'mod_authors': authors, 'mod_credits': credits
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

// Configure jar file
jar {
	archiveName = "${archivesBaseName}-${version}"

	// pack embedded stuff
	from {
        configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "LICENSE.txt", "META-INF/MANIFSET.MF", "META-INF/maven/**", "META-INF/*.RSA", "META-INF/*.SF"
    }

	// configure manifest
    manifest {
        attributes 'MixinConfigs':'mixins.tasmod.json',
                   'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
                   'ForceLoadAsMod': 'true'
    }
    
    
}

// Configure mixin
mixin {
    add sourceSets.main, mixin_refmap
}
