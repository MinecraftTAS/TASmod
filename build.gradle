buildscript {
    repositories {
        maven { url = "https://sizableshrimp.me/maven" }
        maven { url = "https://maven.minecraftforge.net" }
        maven { url = "https://repo.spongepowered.org/maven" }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.3-SNAPSHOT'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
    }
}
plugins {
    id 'com.github.johnrengelman.shadow' version '2.0.4'
    id 'java'
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'


version = "Alpha9"
group = "com.minecrafttas" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "TASmod-1.12.2"

sourceCompatibility = targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}

minecraft {
    version = "1.12.2-14.23.5.2847"
    runDir = "run"
    
    mappings = "snapshot_20171003"
    makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
    replace '${version}':project.version,'${mcversion}':project.minecraft.version
    clientRunArgs += "--mixin mixins.tasmod.json --tweakClass org.spongepowered.asm.launch.MixinTweaker"
    serverRunArgs += "--mixin mixins.tasmod.json --tweakClass org.spongepowered.asm.launch.MixinTweaker"
}

repositories {
    mavenCentral()
    maven { url = "https://maven.mgnet.work/main" }
    maven { url 'https://jitpack.io' }
    maven {
        name = 'sponge'
        url = 'https://repo.spongepowered.org/maven'
    }
}

configurations {
    downloadMod{
    	transitive = false
    }
}

dependencies {
    compile('org.spongepowered:mixin:0.8.2') {
        exclude module: 'launchwrapper'
        exclude module: 'guava'
        exclude module: 'gson'
        exclude module: 'commons-io'
        exclude module: 'log4j-core'
    }
    compile 'org.msgpack:msgpack-core:0.8.16'
    compile 'org.msgpack:jackson-dataformat-msgpack:0.8.16'
    //compile files('libs/KillTheRNG-1.12.2-2.0-WIP-deobf.jar')
    compile 'com.minecrafttas:killtherng:2.0-SNAPSHOT'
    //compile files('libs/BigArrayList-1.3.jar')
    compile 'com.dselent:bigarraylist:1.0'
    
    downloadMod 'com.minecrafttas:killtherng-full:2.0-SNAPSHOT'
}

task downloadKTRNG(type: Copy) {
	group 'tasmod'
	description 'Downloads KillTheRNG to the run/mods/ folder of the project'
    from configurations.downloadMod
    into 'run/mods/'
}

jar {
    manifest {
        attributes 'FMLAT': 'tasmod_at.cfg',
                   'FMLCorePluginContainsFMLMod':'true',
                   'MixinConfigs':'mixins.tasmod.json',
                   'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
                   'ForceLoadAsMod': 'true'
    }
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

shadowJar {
	archiveName = "${archivesBaseName}-${version}-shadow.jar"
    mainSpec.sourcePaths.clear()

    dependsOn reobfJar

    configurations = [project.configurations.compile]
    relocate 'org.msgpack', 'com.minecrafttas.tasmod.repack.org.msgpack'
    relocate 'com.fasterxml', 'com.minecrafttas.tasmod.repack.com.fasterxml'
    classifier "shadow"
    exclude 'dummyThing'
    exclude 'com/minecrafttas/killtherng/', 'assets/killtherng/', 'mixins.killtherng.json', 'mixins.killtherng.refmap.json', 'kaptainwutax/', 'com/seedfinding/'

    afterEvaluate {
        from zipTree(reobfJar.jar)
    }
    
    //Rename delete the mixinless version and rename the shadow jar
    doLast {
    	delete("${buildDir}/libs/${archivesBaseName}-${version}.jar")
    	ant.move(file:"build/libs/${archivesBaseName}-${version}-shadow.jar", tofile:"build/libs/${archivesBaseName}-${version}.jar")
    }
}

artifacts {
    archives shadowJar
}

mixin {
    // Specify "notch" or "searge" here
    defaultObfuscationEnv searge
    add sourceSets.main, "mixins.tasmod.refmap.json"
}
