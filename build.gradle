plugins {
	// loom plugin
    id 'fabric-loom' version "${loom_version}"
}


// Set basic properties
version = project.version
group = project.group
archivesBaseName = project.artifact

// Compile for java 8
sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

// Dependency repositories
repositories {
    mavenCentral()
    maven { url = 'https://raw.githubusercontent.com/BleachDev/cursed-mappings/main/' }
    maven { url = "https://jitpack.io" }
    maven { url = "https://maven.mgnet.work/main" }
    maven { url = 'https://repo.spongepowered.org/maven' }
}

// Dependency configurations
configurations {
    downloadMod {
    	transitive = false
    }
    embed
    implementation.extendsFrom(embed)
}

// Dependencies
dependencies {
	// tasmod dependencies (exported with jar)
    embed group: 'com.dselent', name: 'bigarraylist', version: '1.0'
    
    // add mojang mappings
    mappings "net.legacyfabric:yarn:1.12.2+build.mcp"
    
    // add fabric loader
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

    // add minecraft dependency
    minecraft "com.mojang:minecraft:1.12.2"
    
    // tasmod dependencies (only for compilation)
    compileOnly group: 'com.minecrafttas', name: 'killtherng', version: '2.0'
    
    // tasmod mod dependencies (only for ide)
    downloadMod 'com.minecrafttas:killtherng-full:2.0'
}

// Task for downloading KillTheRng
task downloadKTRNG(type: Copy) {
	group 'tasmod'
	description 'Downloads KillTheRNG to the run/mods/ folder of the project'
    from configurations.downloadMod
    into 'run/mods/'
}

// Update values from mcmod.info
processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", minecraft_version

    from(sourceSets.main.resources.srcDirs) {
        include 'fabric.mod.json'

        expand 'mod_id': project.mod_id, 'mod_url': project.mod_url, 'name': project.mod_name, 'mod_version': project.version,
                'mcversion': project.minecraft_version, 'mod_description': project.mod_description,
                'mod_authors': project.mod_authors, 'mod_credits': project.mod_credits
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'fabric.mod.json'
    }
}

// Configure jar file
jar {
	archivesBaseName = "${archivesBaseName}-${version}"

	// pack embedded stuff
	from {
        configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "LICENSE.txt", "META-INF/MANIFSET.MF", "META-INF/maven/**", "META-INF/*.RSA", "META-INF/*.SF"
    }
}